apply plugin: 'java-library'
apply plugin: 'application'

import proguard.gradle.ProGuardTask

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

sourceSets.main.resources.srcDirs += [ rootProject.file('assets').path ]
sourceCompatibility = 1.8
mainClassName = 'com.squidpony.samples.lwjgl3.Lwjgl3Launcher'
eclipse.project.name = appName + '-desktop'
sourceSets.main.java.srcDirs = [ "src/main/java/" ]

dependencies {
  api("com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion") {
    exclude group: 'org.lwjgl', module: 'lwjgl-openal' //// only removes audio
    exclude group: 'org.jcraft', module: 'jorbis' // removes audio
    exclude group: 'com.badlogicgames.jlayer', module: 'jlayer'
  }
  api "com.badlogicgames.gdx:gdx-lwjgl3-angle:$gdxVersion"
  api "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
  api "com.badlogicgames.gdx:gdx:$gdxVersion"
  api "com.github.SquidPony.SquidLib:squidlib:$squidLibVersion"
  api "com.github.SquidPony.SquidLib:squidlib-util:$squidLibUtilVersion"
  api "com.github.tommyettinger:regexodus:$regExodusVersion"
}

import org.gradle.internal.os.OperatingSystem

run {
  workingDir = rootProject.file('assets').path
  setIgnoreExitValue(true)

  if (OperatingSystem.current() == OperatingSystem.MAC_OS) {
    // Required to run LWJGL3 Java apps on MacOS
    jvmArgs += "-XstartOnFirstThread"
  }
}

jar {
  archiveFileName.set("${appName}.jar")
  duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
  dependsOn configurations.runtimeClasspath
  from { configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
  exclude('META-INF/INDEX.LIST', 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA')
  dependencies {
    exclude('META-INF/INDEX.LIST', 'META-INF/maven/**')
  }
  manifest {
    attributes 'Main-Class': project.mainClassName
  }
  doLast {
    file(archiveFile).setExecutable(true, false)
  }
}
/**
 * Change libraryjars to match your system, or use java.home if possible!
 */
task proguard(type: ProGuardTask) {
  outputs.upToDateWhen { false }
  configuration 'proguard-rules.pro'
//  printmapping 'build/libs/out.map'
  if (System.getProperty('java.version').startsWith('1.')) {
    libraryjars "${System.getProperty('java.home')}/lib/rt.jar"
  } else {
    libraryjars "C:/d/jvm/jdk19-hotspot/jmods/java.base.jmod", jarfilter: '!**.jar', filter: '!module-info.class'
  }
  injars (file("build/libs/${appName}.jar"))

  outjars "build/libs/${appName}-${version}.jar"
}

proguard.dependsOn jar

// Equivalent to the jar task; here for compatibility with gdx-setup.
task dist(dependsOn: [jar]) {
}
