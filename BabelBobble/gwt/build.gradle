buildscript {
	repositories {
		jcenter()
	}
	dependencies {
		classpath 'org.gretty:gretty:3.0.2'
	}
}
apply plugin: "gwt"
apply plugin: "war"
apply plugin: "org.gretty"

gwt {
	gwtVersion = gwtFrameworkVersion // Should match the version used by the libGDX backend.
	maxHeapSize = '1G' // Default 256m is not enough for the GWT compiler. GWT is HUNGRY.
	minHeapSize = '1G'

	src = files(file('src/main/java')) // Needs to be in front of "modules" below.
	modules 'com.github.SquidPony.GdxDefinition'
	devModules 'com.github.SquidPony.GdxDefinitionSuperdev'
	project.webAppDirName = 'webapp'

	compiler.strict = true
	compiler.disableCastChecking = true
	sourceLevel = 1.11
}

dependencies {
//  implementation project(':core')
  implementation "com.badlogicgames.gdx:gdx:$gdxVersion:sources"
	implementation "com.github.tommyettinger:gdx-backend-gwt:1.912.0"
	implementation "com.github.tommyettinger:gdx-backend-gwt:1.912.0:sources"
//	implementation "com.badlogicgames.gdx:gdx-backend-gwt:$gdxVersion"
//  implementation "com.badlogicgames.gdx:gdx-backend-gwt:$gdxVersion:sources"
  //implementation "com.github.SquidPony.SquidLib:squidlib-extra:$squidlibVersion:sources"
	implementation "com.squidpony:squidlib-util:$squidlibVersion"
	implementation "com.squidpony:squidlib-extra:$squidlibVersion"
	implementation "com.squidpony:squidlib-util:$squidlibVersion:sources"
	implementation "com.squidpony:squidlib-extra:$squidlibVersion:sources"
  implementation "com.github.tommyettinger:regexodus:$regExodusVersion:sources"
  //implementation "com.kotcrab.vis:vis-ui:$visUiVersion:sources"
}
import org.akhikhl.gretty.AppBeforeIntegrationTestTask
import org.wisepersist.gradle.plugins.gwt.GwtSuperDev

gretty.httpPort = 8080
gretty.resourceBase = project.buildDir.path + "/gwt/draftOut"
gretty.contextPath = "/"
gretty.portPropertiesFileName = "TEMP_PORTS.properties"

task startHttpServer (dependsOn: [draftCompileGwt]) {
	doFirst {
		copy {
			from "webapp"
			into gretty.resourceBase
		}
		copy {
			from "war"
			into gretty.resourceBase
		}
	}
}
task beforeRun(type: AppBeforeIntegrationTestTask, dependsOn: startHttpServer) {
    // The next line allows ports to be reused instead of
    // needing a process to be manually terminated.
	file("build/TEMP_PORTS.properties").delete()
	// Somewhat of a hack; uses Gretty's support for wrapping a task in
	// a start and then stop of a Jetty server that serves files while
	// also running the SuperDev code server.
	integrationTestTask 'superDev'
	
	interactive false
}

task superDev(type: GwtSuperDev) {
	doFirst {
		gwt.compiler.style = org.wisepersist.gradle.plugins.gwt.Style.DETAILED
		gwt.modules = gwt.devModules
	}
}
task dist(dependsOn: [clean, compileGwt]) {
    doLast {
		file("build/dist").mkdirs()
		copy {
			from "build/gwt/out"
			into "build/dist"
		}
		copy {
			from "webapp"
			into "build/dist"
			}
		copy {
			from "war"
			into "build/dist"
		}
	}
}

sourceSets.main.java.srcDirs = [ "src/main/java/" ]

eclipse.project.name = appName + "-gwt"
