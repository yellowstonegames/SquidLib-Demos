
buildscript {
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath "org.docstr.gwt:org.docstr.gwt.gradle.plugin:$gwtPluginVersion"

    }
}
apply plugin: "org.docstr.gwt"
apply plugin: "war"

// This next line can be changed if you want to, for instance, always build into the
// docs/ folder of a Git repo, which can be set to automatically publish on GitHub Pages.
// This is relative to the html/ folder.
var outputPath = "build/dist/"

gwt {
    gwtVersion = "$gwtFrameworkVersion" // Should match the version used for building the GWT backend. See gradle.properties.
    maxHeapSize = '1G' // Default 256m is not enough for the GWT compiler. GWT is HUNGRY.
    minHeapSize = '1G'

    extraSourceDirs = files(
            file('src/main/java'),
            project(":core").file('src/main/java'),
            files(project(':core').sourceSets.main.allJava.srcDirs),
            files("../core/build/generated/sources/annotationProcessor/java/main"),
            files(sourceSets.main.output.resourcesDir)
    )
    modules = ['com.github.yellowstonegames.GdxDefinition']

    war = file(outputPath)
    compiler.optimize = 9
    compiler.strict = true
    //// The next line can be useful to uncomment if you want output that hasn't been obfuscated.
//  compiler.style = "DETAILED"

    sourceLevel = '1.11'
    devMode {
        port = 8080
        startupUrl = "http://127.0.0.1:8080/index.html"
    }
    superDev {
        modules = ['com.github.yellowstonegames.GdxDefinitionSuperdev']
    }
}

dependencies {
	implementation "com.badlogicgames.gdx:gdx-backend-gwt:$gdxVersion"
	implementation "com.badlogicgames.gdx:gdx-backend-gwt:$gdxVersion:sources"

	implementation "com.badlogicgames.gdx:gdx:$gdxVersion:sources"

    implementation "com.github.tommyettinger:digital:$digitalVersion:sources"
    implementation "com.github.tommyettinger:juniper:$juniperVersion:sources"
    implementation "com.github.tommyettinger:jdkgdxds:$jdkgdxdsVersion:sources"
    implementation "com.github.tommyettinger:crux:0.1.3:sources"
    implementation "com.github.tommyettinger:funderby:$funderbyVersion:sources"
    implementation "com.github.tommyettinger:regexodus:$regExodusVersion:sources"
    implementation "com.github.tommyettinger:textratypist:$textratypistVersion:sources"

    implementation("com.squidpony:squidcore:$squidSquadVersion:sources"){
        exclude module: 'jdkgdxds'
        exclude module: 'juniper'
        exclude module: 'digital'
    }
    implementation("com.squidpony:squidgrid:$squidSquadVersion:sources"){
        exclude module: 'jdkgdxds'
        exclude module: 'juniper'
        exclude module: 'digital'
    }
    implementation("com.squidpony:squidplace:$squidSquadVersion:sources"){
        exclude module: 'jdkgdxds'
        exclude module: 'juniper'
        exclude module: 'digital'
    }
    implementation("com.squidpony:squidseek:$squidSquadVersion:sources"){
        exclude module: 'jdkgdxds'
        exclude module: 'juniper'
        exclude module: 'digital'
    }
    implementation("com.squidpony:squidtext:$squidSquadVersion:sources"){
        exclude module: 'jdkgdxds'
        exclude module: 'juniper'
        exclude module: 'digital'
    }
    implementation("com.squidpony:squidsmooth:$squidSquadVersion:sources"){
        exclude module: 'jdkgdxds'
        exclude module: 'juniper'
        exclude module: 'digital'
    }

//    implementation "com.squidpony:squidcore:$squidSquadVersion:sources"
//    implementation "com.squidpony:squidgrid:$squidSquadVersion:sources"
//    implementation "com.squidpony:squidplace:$squidSquadVersion:sources"
//    implementation "com.squidpony:squidseek:$squidSquadVersion:sources"
//    implementation "com.squidpony:squidtext:$squidSquadVersion:sources"
//    implementation "com.squidpony:squidsmooth:$squidSquadVersion:sources"
    implementation "com.github.tommyettinger:gand:$gandVersion:sources"
    implementation "com.github.tommyettinger:gdcrux:$gdcruxVersion:sources"
//    implementation("com.github.yellowstonegames.squidsquad:squidcore:$squidSquadVersion:sources"){
//        exclude module: 'jdkgdxds'
//        exclude module: 'juniper'
//        exclude module: 'digital'
//    }
//    implementation("com.github.yellowstonegames.squidsquad:squidgrid:$squidSquadVersion:sources"){
//        exclude module: 'jdkgdxds'
//        exclude module: 'juniper'
//        exclude module: 'digital'
//    }
//    implementation("com.github.yellowstonegames.squidsquad:squidplace:$squidSquadVersion:sources"){
//        exclude module: 'jdkgdxds'
//        exclude module: 'juniper'
//        exclude module: 'digital'
//    }
//    implementation("com.github.yellowstonegames.squidsquad:squidseek:$squidSquadVersion:sources"){
//        exclude module: 'jdkgdxds'
//        exclude module: 'juniper'
//        exclude module: 'digital'
//    }
//    implementation("com.github.yellowstonegames.squidsquad:squidtext:$squidSquadVersion:sources"){
//        exclude module: 'jdkgdxds'
//        exclude module: 'juniper'
//        exclude module: 'digital'
//    }
//    implementation("com.github.yellowstonegames.squidsquad:squidsmooth:$squidSquadVersion:sources"){
//        exclude module: 'jdkgdxds'
//        exclude module: 'juniper'
//        exclude module: 'digital'
//    }

//    implementation "com.github.intrigus.gdx-freetype-gwt:gdx-freetype-gwt:$freetypeGwtVersion"
//    implementation "com.github.intrigus.gdx-freetype-gwt:gdx-freetype-gwt:$freetypeGwtVersion:sources"
    implementation "com.github.tommyettinger:gdx-freetype-gwt:$freetypeGwtVersion"
    implementation "com.github.tommyettinger:gdx-freetype-gwt:$freetypeGwtVersion:sources"

	implementation project(':core')
}


//// We delete the (temporary) war/ folder because if any extra files get into it, problems occur.
//// The war/ folder shouldn't be committed to version control.
clean.delete += [file("war")]

tasks.register('dist') {
    dependsOn(["build"])
    doLast {
        // Uncomment the next line if you have changed outputPath and know that its contents
        // should be replaced by a new dist build. Some large JS files are not cleaned up by
        // default unless the outputPath is inside build/ (then the clean task removes them).
        // Do not uncomment the next line if you changed outputPath to a folder that has
        // any files that you want to keep!
        //delete(file(outputPath))

        file(outputPath).mkdirs()
        copy {
            from("build/gwt/out") {
                exclude '**/*.symbolMap' // Not used by a dist, and these can be large.
            }
            into outputPath
        }
        copy {
            from("webapp") {
                exclude 'index.html' // We edit this HTML file later.
                exclude 'refresh.png' // We don't need this button; this saves some bytes.
            }
            into outputPath
        }
        copy {
            from("webapp") {
                // These next two lines take the index.html page and remove the superdev refresh button.
                include 'index.html'
                filter { String line -> line.replaceAll('<a class="superdev" .+', '') }
                // This does not modify the original index.html, only the copy in the dist.
                // If you decide to manually remove or comment out the superdev button from index.html, you should also
                // either remove or comment out only the "filter" line above this.
            }
            into outputPath
        }
        copy {
            from "war"
            into outputPath
        }
    }
}

tasks.register("distZip", Zip) {
    dependsOn("dist")
    //// This uses the output of the dist task, which removes the superdev button from index.html .
    from(outputPath)
    archiveVersion = projectVersion
    archiveBaseName.set("${appName}-dist")
    //// The result will be in html/build/ with a name containing "-dist".
    destinationDirectory.set(file("build"))
}

tasks.register('superDev') {
    dependsOn("dist", "gwtDevMode")
    group("gwt")
}

tasks.compileJava.dependsOn("processResources")

java.sourceCompatibility = JavaVersion.VERSION_11
java.targetCompatibility = JavaVersion.VERSION_11
sourceSets.main.java.srcDirs = [ "src/main/java/" ]

eclipse.project.name = appName + "-html"
