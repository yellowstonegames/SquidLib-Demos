apply plugin: 'application'

import proguard.gradle.ProGuardTask

sourceSets.main.resources.srcDirs += [ rootProject.file('assets').path ]
mainClassName = 'com.github.yellowstonegames.lwjgl3.Lwjgl3Launcher'
eclipse.project.name = appName + '-lwjgl3'
sourceCompatibility = 1.8
java.targetCompatibility = JavaVersion.VERSION_1_8

dependencies {
	implementation("com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion") {
		exclude group: 'org.lwjgl', module: 'lwjgl-openal' //// only removes audio
		exclude group: 'org.jcraft', module: 'jorbis' // removes audio
		exclude group: 'com.badlogicgames.jlayer', module: 'jlayer'
	}
//	implementation "com.badlogicgames.gdx:gdx-lwjgl3-angle:$gdxVersion"

	implementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
	implementation project(':core')
}

def os = System.properties['os.name'].toLowerCase()

run {
	workingDir = rootProject.file('assets').path
	setIgnoreExitValue(true)

	if (os.contains('mac')) {
		// Required to run LWJGL3 Java apps on MacOS
		jvmArgs += "-XstartOnFirstThread"
	}
}

jar {
	archiveFileName.set("${appName}-${version}.jar")
	duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
	dependsOn configurations.runtimeClasspath
	from { configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
	exclude('META-INF/INDEX.LIST', 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA')
	dependencies {
		exclude('META-INF/INDEX.LIST', 'META-INF/maven/**')
	}
	manifest {
		attributes 'Main-Class': project.mainClassName
	}
	doLast {
		file(archiveFile).setExecutable(true, false)
	}
}

task proguard(type: ProGuardTask) {
	outputs.upToDateWhen { false }
	configuration 'proguard-rules.pro'
	//printmapping 'build/lib/out.map'
	if (System.getProperty('java.version').startsWith('1.')) {
		libraryjars "${System.getProperty('java.home')}/lib/rt.jar"
	} else {
		libraryjars "${System.getProperty('java.home')}/jmods/java.base.jmod", jarfilter: '!**.jar', filter: '!module-info.class'
	}
	injars (file("build/libs/${appName}-${version}.jar"))

	outjars "build/libs/${appName}.jar"
}

proguard.dependsOn jar

// Equivalent to the jar task; here for compatibility with gdx-setup.
task dist(dependsOn: [jar]) {
}
